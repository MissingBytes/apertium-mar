DELIMITERS = "<.>" "<!>" "<?>" "<...>" "<Â¶>" ;
SOFT-DELIMITERS = "<,>" ;
SUBREADINGS = LTR ;

SETS

LIST Noun = n ;
LIST Obl = obl ;
LIST Post = post ;
LIST Adj = adj ;
LIST Adv = adv ;
LIST Stem = stem ;
LIST Vaux = vaux ;
LIST Verb = vblex ;
LIST Vblex = vblex ;
LIST Vbser = vbser ;
LIST Det = det ;
LIST Conj = cnjcoo ;
LIST ConjAdv = cnjadv;
LIST Prn = prn ;
LIST Gen = gen ;

SET DetPrn = Det OR Prn ;
SET DetAdjGen = Det OR Adj OR Gen ;


SECTION

#Det if succeeded by N or Adj
SELECT Det IF
    (0 DetPrn)
    (1 Noun) ;

SELECT Det IF
    (0 DetPrn)
    (1 Adj)
    (2 Noun) ;

#Otherwise, choose Prn
SELECT Prn IF
    (0 DetPrn) ;


# 'ti', 'tyachi', 'kali', etc.: fem sg vs. neuter pl

LIST Femsg = (f sg) ;
LIST NounFemsg = (n f sg) ;
LIST Ntpl = (nt pl) ;
LIST NounNtpl = (n nt pl) ;
SET _FemsgNtpl = Femsg OR Ntpl ;
SET FemsgNtpl = _FemsgNtpl + DetAdjGen ;

SELECT Femsg IF
    (0 FemsgNtpl)
    (1 NounFemsg) ;

SELECT Femsg IF
    (0 FemsgNtpl)
    (1 Adj)
    (2 NounFemsg) ;

SELECT Ntpl IF
    (0 FemsgNtpl)
    (1 NounNtpl) ;

SELECT Ntpl IF
    (0 FemsgNtpl)
    (1 Adj)
    (2 NounNtpl) ;

SELECT SUB:1 Femsg IF
    (0/1 FemsgNtpl)
    (1 NounFemsg) ;

SELECT SUB:1 Femsg IF
    (0/1 FemsgNtpl)
    (1 Adj)
    (2 NounFemsg) ;

SELECT SUB:1 Ntpl IF
    (0/1 FemsgNtpl)
    (1 NounNtpl) ;

SELECT SUB:1 Ntpl IF
    (0/1 FemsgNtpl)
    (1 Adj)
    (2 NounNtpl) ;


# 'te', 'tyache', 'kale', etc.: neuter sg vs. masc pl

LIST Ntsg = (nt sg) ;
LIST NounNtsg = (n nt sg) ;
LIST Mpl = (m pl) ;
LIST NounMpl = (n m pl) ;
SET _NtsgMpl = Ntsg OR Mpl ;
SET NtsgMpl = _NtsgMpl + DetAdjGen ;

SELECT Ntsg IF
    (0 NtsgMpl)
    (1 NounNtsg) ;

SELECT Ntsg IF
    (0 NtsgMpl)
    (1 Adj)
    (2 NounNtsg) ;

SELECT Mpl IF
    (0 NtsgMpl)
    (1 NounMpl) ;

SELECT Mpl IF
    (0 NtsgMpl)
    (1 Adj)
    (2 NounMpl) ;

SELECT SUB:1 Ntsg IF
    (0/1 NtsgMpl)
    (1 NounNtsg) ;

SELECT SUB:1 Ntsg IF
    (0/1 NtsgMpl)
    (1 Adj)
    (2 NounNtsg) ;

SELECT SUB:1 Mpl IF
    (0/1 NtsgMpl)
    (1 NounMpl) ;

SELECT SUB:1 Mpl IF
    (0/1 NtsgMpl)
    (1 Adj)
    (2 NounMpl) ;


#'tya', 'tyachya', 'kalya', etc.: fem nom pl vs. obl

LIST FemplNom = (f pl nom) ;
LIST NounFemplNom = (n f pl nom) ;
LIST Sgsp = sg sp ;
SET SgObl = (mfn obl) + Sgsp ;
LIST NounSgObl = (n sg obl) ;
LIST Plsp = pl sp ;
SET PlObl = (mfn obl) + Plsp ;
LIST NounPlObl = (n pl obl) ;
LIST Sgplsp = sg pl sp ;
LIST NounObl = (n obl) ;
SET MfnObl = (mfn obl) + Sgplsp ;
SET _FemplnomMfnobl = FemplNom OR MfnObl ;
SET FemplnomMfnobl = _FemplnomMfnobl + DetAdjGen ;

SELECT FemplNom IF
	(0 FemplnomMfnobl)
	(1 NounFemplNom) ;

SELECT PlObl IF
	(0 FemplnomMfnobl)
	(1 NounPlObl) ;

SELECT SgObl IF
	(0 FemplnomMfnobl)
	(1 NounSgObl) ;

SELECT FemplNom IF
	(0 FemplnomMfnobl)
    (1 Adj)
	(2 NounFemplNom) ;

SELECT PlObl IF
	(0 FemplnomMfnobl)
    (1 Adj)
	(2 NounPlObl) ;

SELECT SgObl IF
	(0 FemplnomMfnobl)
    (1 Adj)
	(2 NounSgObl) ;

SELECT SUB:1 FemplNom IF
	(0/1 FemplnomMfnobl)
	(1 NounFemplNom) ;

SELECT SUB:1 MfnObl IF
	(0/1 FemplnomMfnobl)
	(1 NounObl) ;

SELECT SUB:1 FemplNom IF
	(0/1 FemplnomMfnobl)
    (1 Adj)
	(2 NounFemplNom) ;

SELECT SUB:1 MfnObl IF
	(0/1 FemplnomMfnobl)
    (1 Adj)
    (2 NounObl) ;
